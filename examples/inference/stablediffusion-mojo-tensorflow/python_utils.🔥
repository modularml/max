# ===----------------------------------------------------------------------=== #
# Copyright (c) 2024, Modular Inc. All rights reserved.
#
# Licensed under the Apache License v2.0 with LLVM Exceptions:
# https://llvm.org/LICENSE.txt
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
# ===----------------------------------------------------------------------=== #

# ===----------------------------------------------------------------------=== #
#
# This file is Modular Inc proprietary.
#
# ===----------------------------------------------------------------------=== #

import python
from memory import memcpy
from tensor import Tensor, TensorShape
from collections import List


fn expandvars(path: String) raises -> String:
    var os = python.Python.import_module("os")
    return str(os.path.expandvars(path))


fn add_to_path(path: String) raises:
    var path_obj = expandvars(path)
    python.Python.add_to_path(path_obj)
    print("Added", path_obj, "to Python path")


@always_inline
fn numpy_data_pointer[
    type: DType
](numpy_array: PythonObject) raises -> DTypePointer[type]:
    return DTypePointer[type](
        __mlir_op.`pop.index_to_pointer`[
            _type = __mlir_type[`!kgen.pointer<scalar<`, type.value, `>>`]
        ](
            Scalar[DType.index](
                numpy_array.__array_interface__["data"][0].__index__()
            ).value
        )
    )


@always_inline
fn memcpy_to_numpy(array: PythonObject, tensor: Tensor) raises:
    var dst = numpy_data_pointer[tensor.dtype](array)
    var src = tensor._ptr
    var length = tensor.num_elements()
    memcpy(dst, src, length)


@always_inline
fn memcpy_from_numpy(array: PythonObject, tensor: Tensor) raises:
    var src = numpy_data_pointer[tensor.dtype](array)
    var dst = tensor._ptr
    var length = tensor.num_elements()
    memcpy(dst, src, length)


@always_inline
fn shape_to_python_list(shape: TensorShape) raises -> PythonObject:
    var python_list = python.Python.evaluate("list()")
    for i in range(shape.rank()):
        _ = python_list.append(shape[i])
    return python_list ^


@always_inline
fn get_np_dtype[type: DType](np: PythonObject) raises -> PythonObject:
    @parameter
    if type.is_float32():
        return np.float32
    elif type.is_int32():
        return np.int32
    elif type.is_int64():
        return np.int64
    elif type.is_uint8():
        return np.uint8

    raise "Unknown datatype"


@always_inline
fn tensor_to_numpy[
    type: DType
](tensor: Tensor[type], np: PythonObject) raises -> PythonObject:
    var shape = shape_to_python_list(tensor.shape())
    var tensor_as_numpy = np.zeros(shape, get_np_dtype[type](np))
    _ = shape ^
    memcpy_to_numpy(tensor_as_numpy, tensor)
    return tensor_as_numpy ^


@always_inline
fn numpy_to_tensor[type: DType](array: PythonObject) raises -> Tensor[type]:
    var shape = List[Int]()
    var array_shape = array.shape
    for dim in array_shape:
        shape.push_back(dim.__index__())
    var out = Tensor[type](shape)
    memcpy_from_numpy(array, out)
    return out ^
